// project dependencies and tasks

plugins {
	// java compilation tasks like: compileJava, test, jar, build
	id 'java'
	// spring boot tasks like: bootRun, bootJar, bootWar, bootTest, bootTestReport
	id 'org.springframework.boot' version '4.0.2'
	// spring dependency management tasks like: resolve, clean, verify, compileJava, compileTestJava, processResources, processTestResources, testCompile, testRuntime, bootRun, bootJar, bootWar, bootTest, bootTestReport
	id 'io.spring.dependency-management' version '1.1.7'
	// code coverage reporting: jacocoTestReport, jacocoTestCoverageVerification
	id 'jacoco'
}

group = 'io.github.danny270793.analytics'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		// this must be build with java 17
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	/*
	implementation
		to libraries directly used on my source code, that must be available on runtime
			compile + runtime
			included on jar
	runtimeOnly
		to libraries loaded dynamically on runtime, that must be available on runtime
			runtime
			included on jar
	developmentOnly
		development tools that shoulden go to production
			compile + runtime(dev)
			not included on jar
	testImplementation
		to libraries used for testing, directly used on my source code
			compile + runtime(test)
			not included on jar
	testRuntimeOnly
		to libraries used for testing loaded dynamically, that must be available on test runtime
			runtime(test)
			not included on jar
	*/

	// hot reload support
	developmentOnly("org.springframework.boot:spring-boot-devtools")
	// controllers, tomcat, json serializer/deserializer: @RestController, @GetMapping
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	// spring data jpa, hibernate orm, transactions: @Entity, JpaRepository
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// hibernate/jakarta validations: @Valid, @NotNull, @Email
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	// bcrypt, authentication, authorization
	implementation 'org.springframework.boot:spring-boot-starter-security'
	// health endpoints
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// database migrations
	implementation 'org.springframework.boot:spring-boot-starter-liquibase'
	// openapi/swagger documentation - using 2.7.0 for Spring Boot 4.x compatibility
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
	// interface fot jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
	// jwt implementation
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
	// jwt jackson serializer/deserializer
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'
	// postgresql
	runtimeOnly 'org.postgresql:postgresql'
	// h2 database for testing
	developmentOnly 'com.h2database:h2'
	// h2 database for testing
	testRuntimeOnly 'com.h2database:h2'
	// testing tools for spring boot
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// junit 5 test runner
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	// mockito for testing
	testImplementation 'org.mockito:mockito-core'
	// spring security test
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	// to task called test, use junit platform launcher
	useJUnitPlatform()
	// generate jacoco execution data for coverage report
	finalizedBy jacocoTestReport
}

jacoco {
	// jacoco version compatible with Java 17
	toolVersion = "0.8.11"
}

jacocoTestReport {
	// generate coverage report after tests
	dependsOn test
	
	reports {
		// generate xml report for CI/CD tools
		xml.required = true
		// generate html report for human readable coverage
		html.required = true
		// csv report disabled
		csv.required = false
		
		// html report location
		html.outputLocation = layout.buildDirectory.dir('reports/jacoco/html')
	}
	
	// exclude generated code and DTOs from coverage
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/dto/**',
				'**/config/**',
				'**/BackendApplication.class'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			// minimum coverage thresholds
			limit {
				minimum = 0.70 // 70% minimum coverage
			}
		}
		
		rule {
			element = 'CLASS'
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.60 // 60% per class minimum
			}
			// exclude DTOs and configs from class-level verification
			excludes = [
				'*.dto.*',
				'*.config.*',
				'*.BackendApplication'
			]
		}
	}
}

// run coverage verification after test report
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport
